apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        // Package name format: com.by_syk.nanoiconpack.[name_of_your_icon_pack]
        applicationId "com.by_syk.nanoiconpack.sample"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 17013101
        // Version name format: [version].[num_of_icons]
        versionName "1.2.0.1.nightly"
    }

    buildTypes {
        release {
            shrinkResources false // Keep false, or icons in /drawable-nodpi/ will become black.
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // For formatting apk file name, like com.by_syk.template_v1.0.0(17012500).apk
            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null) {
                        def fileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}(${defaultConfig.versionCode}).apk"
                        output.outputFile = new File((String) outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

/*repositories {
    flatDir { dirs 'libs' }
}*/

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'junit:junit:4.12'

    compile project(':nanoiconpack')
}
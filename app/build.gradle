apply plugin: 'com.android.application'

android {
    compileSdkVersion Integer.parseInt(project.COMPILE_SDK_VERSION)
    buildToolsVersion project.BUILD_TOOLS_VERSION

    defaultConfig {
        // Package name format: com.[your_name].iconpack.[name_of_your_icon_pack]
        applicationId "com.by_syk.nanoiconpack.sample"
        minSdkVersion Integer.parseInt(project.MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.TARGET_SDK_VERSION)
        versionCode 17062400
        // Version name format: [version].[num_of_icons]
        versionName "3.0.0.7.nightly"

        resConfigs "en", "zh_cn"
    }

    buildTypes {
        release {
            shrinkResources false // Keep false, or icons in /drawable-nodpi/ will become black.
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // For formatting apk file name, like com.by_syk.template_v1.0.0(17012500).apk
            android.applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null) {
                        def fileName = "${defaultConfig.applicationId}_v${defaultConfig.versionName}(${defaultConfig.versionCode}).apk"
                        output.outputFile = new File((String) outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

/*repositories {
    flatDir { dirs 'libs' }
}*/

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    testCompile 'junit:junit:4.12'

    compile project(':nanoiconpack')
}